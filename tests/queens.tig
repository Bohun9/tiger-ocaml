let 
    var n := 8

    type intarray = array of int

    var row := intarray [n] of 0
    var col := intarray [n] of 0
    var diag1 := intarray [n+n-1] of 0
    var diag2 := intarray [n+n-1] of 0

    function printboard() = 
        (for r := 0 to n-1 do
            (for c := 0 to n-1 do
                print(if col[c] = r then " O" else " .")
            ; print("\n")
            )
        ; print("\n"))

    function solve(c:int) = 
        if c = n then
            printboard()
        else
            for r := 0 to n-1 do
                if row[r] = 0 & diag1[r+c] = 0 & diag2[r+7-c] = 0 then (
                    row[r] := 1; diag1[r+c] := 1; diag2[r+7-c] := 1;
                    col[c] := r;
                    solve(c+1);
                    row[r] := 0; diag1[r+c] := 0; diag2[r+7-c] := 0
                )
in
    solve(0)
end
